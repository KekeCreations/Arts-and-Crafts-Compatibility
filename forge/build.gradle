plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle'
    id 'org.spongepowered.mixin'
}
base {
    archivesName = "${mod_id}-forge-${minecraft_version}"
}
mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    copyIdeResources = true //Calls processResources when in dev

    // Automatically enable forge AccessTransformers if the file exists
    // This location is hardcoded in Forge and can not be changed.
    // https://github.com/MinecraftForge/MinecraftForge/blob/be1698bb1554f9c8fa2f58e32b9ab70bc4385e60/fmlloader/src/main/java/net/minecraftforge/fml/loading/moddiscovery/ModFile.java#L123
    if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
        accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    }

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":common").sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly project(":common")
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
    implementation fg.deobf("curse.maven:artsandcrafts-1034791:5744051")

    //GOTTA KEEP FOR CODE
    implementation fg.deobf("curse.maven:decorative-blocks-362528:5089406")
    implementation fg.deobf("curse.maven:ecologics-570463:4857272")
    implementation fg.deobf("curse.maven:dramatic-doors-380617:5476685")

    //OPTIONAL
    implementation fg.deobf("curse.maven:gilded-sherds-935749:4975225")
    implementation fg.deobf("curse.maven:oh-my-sherd-897227:4864087")
    implementation fg.deobf("curse.maven:built-982827:5398958")
    implementation fg.deobf("curse.maven:twigs-496913:4998195")
    implementation fg.deobf("curse.maven:farmers-delight-398521:5051242")
    implementation fg.deobf("curse.maven:neapolitan-382016:5296594")
    implementation fg.deobf("curse.maven:blueprint-382216:5292242")
    implementation fg.deobf("curse.maven:built-982827:5398958")
    implementation fg.deobf("curse.maven:more-pottery-sherd-909168:4733392")
    implementation fg.deobf("curse.maven:sherdsapi-847583:4931972")
    //implementation fg.deobf("curse.maven:cobblemon-687131:5375390")
    implementation fg.deobf("curse.maven:kotlin-for-forge-351264:5402061")
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
            fg.component(it)
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
